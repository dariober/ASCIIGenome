apply plugin: 'java'
apply plugin: 'jacoco'

def jversion= JavaVersion.VERSION_1_8

sourceCompatibility= jversion 
targetCompatibility= jversion

if(JavaVersion.current() < jversion) {
	throw new GradleException("Java >= " + jversion + " is required but you are running " + JavaVersion.current())
}

/** Return the MD5 sum of the give File object */
ext.generateMD5 = { file ->
	def digest = java.security.MessageDigest.getInstance("MD5")
	file.eachByte( 4096 ) { buffer, length ->
		digest.update( buffer, 0, length )
	}
	new BigInteger(1, digest.digest()).toString(16).padLeft(32, '0')
}

/** Gunzip the given file. Same as `gunzip -c dir/myfile.txt.gz > dir/myfile.txt`. 
String filename must have extension .gz */
ext.gunzip = { filename ->
    file(filename.replaceFirst('\\.gz$', '')).withOutputStream { os ->
  	  os << resources.gzip(filename).read()
  	}
}

/* Prepare test data
 */
File targetFile= new File('test_data/chr7.fa')
String targetMD5= "30c3693ead968844a769a90a801a900f"

if (!targetFile.exists() || ext.generateMD5(targetFile) != targetMD5) {
	File fgz= new File("test_data", targetFile.name + ".gz")
    ext.gunzip(fgz.absolutePath)
    if(ext.generateMD5(targetFile) != targetMD5){
        throw new GradleException("Corrupted file " + targetFile)
    }
}

targetFile= new File('test_data/ALL.wex.union_illumina_wcmc_bcm_bc_bi.20110521.snps.exome.sites.vcf')
targetMD5= "6783ca5e6df2ce1075e1e0c924ad7d37"

if (!targetFile.exists() || ext.generateMD5(targetFile) != targetMD5) {
    File fgz= new File("test_data", targetFile.name + ".gz")
    ext.gunzip(fgz.absolutePath)
    if(ext.generateMD5(targetFile) != targetMD5){
        throw new GradleException("Corrupted file " + targetFile)
    }
}

ext.gunzip("test_data/CEU.exon.2010_06.genotypes.vcf.gz")
ext.gunzip("test_data/ALL.wgs.mergedSV.v8.20130502.svs.genotypes.vcf.gz")

/* ------ */

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

jar {
    manifest {
        attributes "Main-Class": "samTextViewer.Main"
    }
 
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}


dependencies {
	
	compile group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
	compile group: 'jline', name: 'jline', version: '2.14.5'
    compile 'com.github.dariober:Jawk:e5c8f26'
    compile 'com.github.dariober:htsjdk:37d8bd9'
	compile 'com.github.igvteam:igv:v2.4.10'

	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
	compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
	compile group: 'commons-validator', name: 'commons-validator', version: '1.6'
	compile group: 'commons-io', name: 'commons-io', version: '2.6'
	
	compile group: 'com.esotericsoftware.yamlbeans', name: 'yamlbeans', version: '1.13'
	compile group: 'com.itextpdf', name: 'itextpdf', version: '5.5.13'
	compile group: 'com.google.guava', name: 'guava', version: '24.0-android'
	compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
	compile group: 'org.biojava', name: 'biojava-genome', version: '4.2.11'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
	compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.21.0.1'
	
    testCompile 'junit:junit:4.12'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
